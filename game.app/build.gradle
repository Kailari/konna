import org.gradle.internal.os.OperatingSystem


def buildPlatformName = System.getenv("TARGET_PLATFORM")
def buildPlatform = buildPlatformName != null ? OperatingSystem.forName(buildPlatformName) : null
if (buildPlatform == null) {
    buildPlatform = OperatingSystem.current()
}

switch (buildPlatform) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

dependencies {
    implementation project(':engine')
    implementation project(':engine.ecs')
    implementation project(':engine.utilities')
    implementation project(':engine.view')
    implementation project(':engine.lwjgl')

    implementation project(':game')
    implementation project(':game.view')

    implementation "org.joml:joml:${jomlVersion}"

    // "org.lwjgl:lwjgl" is required for MemoryUtil and MemoryStack
    // other lwjgl modules are there just for running tests with the full renderer
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    for (lwjglModule in ["", "-assimp", "-glfw", "-openal", "-opengl", "-stb", "-opengl"]) {
        testImplementation "org.lwjgl:lwjgl${lwjglModule}"
        testRuntime "org.lwjgl:lwjgl${lwjglModule}::${lwjglNatives}"
    }

    // Cucumber for fancy tests
    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${jUnitVersion}"
}

test {
    moduleOptions {
        runOnClasspath = true
    }
}
