plugins{
    id "application"
    id "java"
    id "com.github.johnrengelman.shadow" version '5.1.0'
}

dependencies {
    implementation project(':engine')
    implementation project(':engine.lwjgl')
    implementation project(':game')

    // LWJGL and its components
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-opengl"
    for (lwjglNatives in ["natives-linux", "natives-macos", "natives-windows"]) {
        runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    }
    implementation "org.joml:joml:${jomlVersion}"
}

application {
    mainClassName = "fi.jakojaannos.roguelite.launcher.Main"
    //applicationDefaultJvmArgs = ['-XX:+PrintGCDetails']
}

run {
    moduleOptions {
        addModules = [
                "org.lwjgl.natives",
                "org.lwjgl.opengl.natives",
                "org.lwjgl.glfw.natives",
        ]
        addReads = ["launcher": "org.lwjgl.natives,org.lwjgl.opengl.natives,org.lwjgl.glfw.natives"]
    }

    //jvmArgs = ['-XX:+PrintGCDetails']
    setArgsString("--debug --assetRoot ../assets/")
}

jar {
    manifest {
        attributes "Main-Class": "fi.jakojaannos.roguelite.launcher.Main"
    }
}


test {
    moduleOptions {
        runOnClasspath = true
    }
}

