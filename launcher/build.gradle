import org.gradle.internal.os.OperatingSystem

plugins {
    id "application"
    id "java"
    id "org.beryx.jlink" version "2.17.6"
}

final def buildPlatformName = System.getenv("TARGET_PLATFORM")
def buildPlatform = buildPlatformName != null ? OperatingSystem.forName(buildPlatformName) : null
if (buildPlatform == null) {
    buildPlatform = OperatingSystem.current()
}

def final runtimeModules = ["", "glfw", "shaderc", "stb", "assimp", "openal"]
def final implementationModules = [*runtimeModules, 'vulkan']

dependencies {
    implementation project(':game.app')

    implementation "org.joml:joml:${jomlVersion}"

    // LWJGL and its components
    implementation platform("org.lwjgl:lwjgl-bom:${lwjglVersion}")
    final def lwjglNatives = selectNatives(buildPlatform)
    for (final modulePostfix in implementationModules) {
        implementation "org.lwjgl:lwjgl${prefixIfNotEmpty(modulePostfix, "-")}"
    }
    for (final modulePostfix in runtimeModules) {
        implementation "org.lwjgl:lwjgl${prefixIfNotEmpty(modulePostfix, "-")}::${lwjglNatives}"
    }
    if (lwjglNatives == "natives-macos") runtimeOnly "org.lwjgl:lwjgl-vulkan::${lwjglNatives}"
}

application {
    mainClassName = "roguelite.launcher/fi.jakojaannos.roguelite.launcher.Main"
    //applicationDefaultJvmArgs = ['-XX:+PrintGCDetails']
}

jlink {
    // XXX: Stripping debug symbols makes classloader fail on some records
    addOptions /*'--strip-debug', */ '--compress', '2', '--no-header-files', '--no-man-pages'
    forceMerge "log4j-api"
    launcher {
        name = 'konna'
        jvmArgs = ['--enable-preview']
    }
}

def targetAssetsPath = "${buildDir}/image"

// FIXME: Just pass the JDK path via command line arguments, defaulting to something sensible, and be done with it
if (buildPlatformName != null) {
    jlink {
        final def CI = System.getenv("CI")
        final def path = (CI != null && CI == "true")
                ? "${System.getenv("HOME")}/repo/jdk/jdk-${buildPlatformName}/jdk-14/"
                : getPlatformHomeWithFallBack(buildPlatformName)
        if (path == null) {
            throw new IllegalStateException("Could not determine JDK location for platform \"${buildPlatformName}\"")
        }
        targetPlatform(buildPlatformName, path)
    }

    targetAssetsPath += "/konna-${buildPlatformName}"
}

tasks.jlink.doLast {
    copy {
        from('../assets')
        into("${targetAssetsPath}/bin/assets")
        exclude("**/*.aseprite")
    }
}

run {
    //jvmArgs = ['-XX:+PrintGCDetails']
    setArgsString("--debug --assetRoot ../assets/")
}

task runClient {
    group 'application'

    doFirst { run.setArgsString("--debug --assetRoot ../assets/ --client localhost 18181") }
    finalizedBy(run)
}

task runServer {
    group 'application'

    doFirst { run.setArgsString("--debug --assetRoot ../assets/ --server 18181") }
    finalizedBy(run)
}

jar {
    manifest {
        attributes "Main-Class": "fi.jakojaannos.roguelite.launcher.Main"
    }
}

static String selectNatives(final OperatingSystem platform) {
    switch (platform) {
        case OperatingSystem.LINUX:
            return "natives-linux"
        case OperatingSystem.MAC_OS:
            return "natives-macos"
        case OperatingSystem.WINDOWS:
            return "natives-windows"
        default:
            throw new UnsupportedOperationException("Unknown build platform: \"${platform}\"")
    }
}

static String getPlatformHomeWithFallBack(final String platformName) {
    final def path = System.getenv("JAVA_HOME_${platformName.toUpperCase()}")
    return path != null ? path : System.getenv("JAVA_HOME")
}

static String prefixIfNotEmpty(final String string, final String prefix) {
    return string.isEmpty()
            ? ""
            : "${prefix}${string}"
}
